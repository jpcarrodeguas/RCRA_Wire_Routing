% AUTORES
% Xin Lin
% JuliÃ¡n Penedo Carrodeguas

#program initial.
#const numrows = 12.
#const numcols = 20.
#const numwires = 5.

row(1..numrows).
col(1..numcols).

% Directions
dir(north;east;south;west).
opdir(north,south;east,west;south,north;west,east).

% Assume two initial points, select one currentpoint (initial) and one endpoint (goal)
1 { currentpoint(R,C,T) : _point(R,C,T) } 1 :- type(T).
1 { endpoint(R,C,T) : _point(R,C,T), not currentpoint(R,C,T) } 1 :- type(T).

#program dynamic.
% Action generation
1 { move(T,D) : _dir(D) } 1 :- _type(T), not 'reached(T).

% Executability axioms
:- 'reached(T), move(T,_).
:- 'move(T,D1), move(T,D2), _opdir(D1,D2).

% Effect axioms
currentpoint(R-1,C,T) :- 'currentpoint(R,C,T), move(T,north), _row(R-1).
currentpoint(R,C+1,T) :- 'currentpoint(R,C,T), move(T,east), _col(C+1).
currentpoint(R+1,C,T) :- 'currentpoint(R,C,T), move(T,south), _row(R+1).
currentpoint(R,C-1,T) :- 'currentpoint(R,C,T), move(T,west), _col(C-1).

% Executability constraints
:- _obstacle(R,C), currentpoint(R,C,T).
:- 'point(R,C,T1), point(R,C,T2), T1!=T2.
:- vecinos(R,C,T,N), point(R,C,T), N > 2.
:- vecinos(R,C,T,N), currentpoint(R,C,T), N!=1.
:- ladometa(R,C,T,D), not reached(T).

vecino(R,C,T,west) :- point(R,C,T), point(R,C-1,T), not _endpoint(R,C-1,T).
vecino(R,C,T,east) :- point(R,C,T), point(R,C+1,T), not _endpoint(R,C+1,T).
vecino(R,C,T,north) :- point(R,C,T), point(R-1,C,T), not _endpoint(R-1,C,T).
vecino(R,C,T,south) :- point(R,C,T), point(R+1,C,T), not _endpoint(R+1,C,T).
vecinos(R,C,T,N) :- N = #count{D : vecino(R,C,T,D), _dir(D)}, point(R,C,T).

ladometa(R,C,T,west) :- point(R,C,T), _endpoint(R,C-1,T), not 'reached(T).
ladometa(R,C,T,east) :- point(R,C,T), _endpoint(R,C+1,T), not 'reached(T).
ladometa(R,C,T,north) :- point(R,C,T), _endpoint(R-1,C,T), not 'reached(T).
ladometa(R,C,T,south) :- point(R,C,T), _endpoint(R+1,C,T), not 'reached(T).

% Inertia
point(R,C,T) :- 'point(R,C,T).
point(R,C,T) :- 'currentpoint(R,C,T).
reached(T) :- 'reached(T).
reached(T) :- currentpoint(R,C,T), _endpoint(R,C,T).

#program final.
goal :- numwires = #count{T : reached(T)}.
:- not goal.

% Get minimal number of wires
countwires(N) :- N = #count{coords(R,C) : point(R,C,T), _type(T)}.
#minimize{N: countwires(N)}.

#show currentpoint/3.

% Debug
%#show countwires/1.
%#show reached/1.
%#show move/2.
%#show obstacle/2.
%#show currentpoint/3.
%#show point/3.
%#show endpoint/3.
%#show vecinos/4.

